bind = import("bind")
time = import("time")
strings = import("strings")

load("scripts/definitions/prayer.ank")


bind.onLogin(func(player) {
	prayers = []
	for i in range(14) {
		prayers[i] = {
			"drained": defs[i].drained,
			"drainDuration": defs[i].drainDuration,
			"startTime": defs[i].startTime,
		}
	}

	player.Tickables += func() {
		for index in range(14) {
			if !prayers[index].startTime.IsZero() {
				// prayer active
				if time.Since(prayers[index].startTime).Milliseconds() >= prayers[index].drainDuration(player.PrayerPoints()) {
					// drain a level
					prayers[index].startTime = prayers[index].startTime.Add(time.Millisecond * prayers[index].drainDuration(player.PrayerPoints()))
					player.IncCurStat(PRAYER, -1)
					if player.Skills().Current(PRAYER) <= 0 {
						player.Message("You have run out of prayer points. Return to a church to recharge")
						for idx in range(14) {
							player.PrayerOff(idx)
							prayers[idx] = {
								"drained": defs[idx].drained,
								"drainDuration": defs[idx].drainDuration,
								"startTime": defs[idx].startTime,
							}
						}
						player.SendPrayers()
						break
					}
				}

				if !player.PrayerActivated(index) {
					// player turning prayer off
					prayers[index].drained = time.Since(prayers[index].startTime).Milliseconds()
					prayers[index].startTime = defs[index].startTime
//				} else {
//					println((time.Since(prayers[index].startTime).Milliseconds() / prayers[index].drainDuration(player.PrayerPoints()) * 100) + "% drained (" + time.Since(prayers[index].startTime).Milliseconds() + "/" + prayers[index].drainDuration(player.PrayerPoints()) + "ms) for prayer [" + index + "]")
				}
			} else {
				// prayer inactive
				if player.PrayerActivated(index) {
					// player turning prayer on
					prayers[index].startTime = time.Now().Add(-time.Millisecond * prayers[index].drained)
					prayers[index].drained = defs[index].drained
//					println((time.Since(prayers[index].startTime).Milliseconds() / prayers[index].drainDuration(player.PrayerPoints()) * 100) + "% drained (" + time.Since(prayers[index].startTime).Milliseconds() + "/" + prayers[index].drainDuration(player.PrayerPoints()) + "ms) for prayer [" + index + "]")
				}
			}
		}
	}
})

bind.item(itemPredicate(20, 413, 604, 814), func(player, item) {
	player.Message("You dig a hole in the ground")
	sleep(tMillis*640)
	player.Message("You bury the " + strings.ToLower(item.Name()))
	player.Inventory.RemoveByID(toInt(item.ID), 1)
	switch item.ID {
	case 20:
		player.IncExp(PRAYER, 15/4)
	case 604:
		player.IncExp(PRAYER, 18/4)
	case 413:
		player.IncExp(PRAYER, 50/4)
	case 814:
		player.IncExp(PRAYER, 240/4)
	}
})
