bind = import("bind")
time = import("time")
strings = import("strings")

load("scripts/definitions/prayer.ank")

bind.onLogin(func(player) {
	drained = 0
	player.Tickables.Add(func() {
		index = 0
		for drainRate in drainRates {
			if player.PrayerActivated(index) {
				drained += drainRate
				if drained >= 325 {
					drained -= 325
					player.IncCurStat(PRAYER, -1)
					if player.Skills().Current(PRAYER) <= 0 {
						player.Message("You have run out of prayer points. Return to a church to recharge")
						for idx in range(14) {
							player.PrayerOff(idx)
						}
						player.SendPrayers()
					}
				}
			}
			index++
		}
	})
})

bind.item(itemPredicate(20, 413, 604, 814), func(player, item) {
	player.Message("You dig a hole in the ground")
	sleep(tMillis*640)
	player.Message("You bury the " + strings.ToLower(item.Name()))
	player.Inventory.RemoveByID(toInt(item.ID), 1)
	switch item.ID {
	case 20:
		player.IncExp(PRAYER, 15/4)
	case 604:
		player.IncExp(PRAYER, 18/4)
	case 413:
		player.IncExp(PRAYER, 50/4)
	case 814:
		player.IncExp(PRAYER, 240/4)
	}
})
