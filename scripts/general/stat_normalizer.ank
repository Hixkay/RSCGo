bind = import("bind")
math = import("math")
time = import("time")

bind.onLogin(func(player) {
	curTick = 0
	player.Tickables += func() {
		curTick++
		if time.Since(player.Transients().VarTime("deathTime")) <= time.Second*5 {
			// Hack to fix bug in client death screen that freezes client if no new player position packets arrive after
			// the timer runs out.  Timer for it is 250, and decreases at a rate of 50 per sec, e.g 5 secs
			player.ResetTickables += func() {
				player.SetSpriteUpdated()
			}
		}
		skillsTimer = player.PrayerActivated(PRAYER_RAPID_RESTORE) ? 50 : 100
		hitpointTimer = player.PrayerActivated(PRAYER_RAPID_HEAL) ? 50 : 100
		normalize = func(idx) {
			delta = player.Skills().DeltaMax(idx)
			if math.Abs(delta) != 0 {
				player.IncCurStat(idx, delta / math.Abs(delta))
			}
		}

		if curTick % skillsTimer == 0 {
			for i in range(THIEVING) {
				if i == HITPOINTS || i == PRAYER || player.Skills().DeltaMax(i) == 0 {
					// Handled elsewhere
					continue
				}
				normalize(i)
				if player.Skills().DeltaMax(i) == 0 {
					player.Message("Your " + skillName(i) + " level has returned to normal")
				}
			}
		}

		if curTick % hitpointTimer == 0 {
			normalize(HITPOINTS)
		}
	}
})
