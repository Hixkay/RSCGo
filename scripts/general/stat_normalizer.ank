bind = import("bind")
math = import("math")
time = import("time")

bind.onLogin(func(player) {
	normalize = func(idx, notify) {
		max = player.Skills().Maximum(idx)
		cur = player.Skills().Current(idx)
	
		if cur == max {
			return
		}
		
		if cur < max {
			player.SetCurStat(idx, cur+1)
		} else if cur > max {
			player.SetCurStat(idx, cur-1)
		}
		
		if notify && player.Skills().Current(idx) == max {
			player.Message("Your " + skillName(idx) + " level has returned to normal")
		}
	}
	curTick = 0
	player.Tickables += func() {
		curTick++
		if time.Since(player.Transients().VarTime("deathTime")) <= time.Second*5 {
			// Hack to fix bug in client death screen
			player.ResetTickables += func() {
				player.SetSpriteUpdated()
			}
		}
		skillTime = player.PrayerActivated(6) ? 50 : 100
		hitsTime = player.PrayerActivated(7) ? 50 : 100
		if curTick % skillTime == 0 {
			for i = 0; i < 18; i++ {
				if i == HITPOINTS || i == PRAYER {
					// Handled elsewhere
					continue
				}
				normalize(i, true)
			}
		}
		if curTick % hitsTime == 0 {
			normalize(HITPOINTS, false)
		}
	}
})